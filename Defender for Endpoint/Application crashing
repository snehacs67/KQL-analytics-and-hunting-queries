Description: This query identifies crashing processes based on parameters passed to werfault.exe and attempts to find the associated process launch

tactics:
  - Execution, Misconfiguration 
  - DefenseEvasion
techniques:
  - T1203    # Exploitation for Client Execution
  - T1036    # Masquerading
  - T1499    # Endpoint Denial of Service

Query: 
  DeviceProcessEvents
  | where Timestamp > ago(1d)
  | where FileName =~ 'werfault.exe'
  | project CrashTime = Timestamp, DeviceId, WerFaultCommand = ProcessCommandLine, CrashProcessId = extract("-p ([0-9]{1,5})", 1, ProcessCommandLine) 
  | join kind= inner hint.strategy=shuffle DeviceProcessEvents on DeviceId
  | where CrashProcessId == ProcessId and Timestamp between (datetime_add('day',-1,CrashTime) .. CrashTime)
  | project-away ActionType
  | project-rename ProcessStartTimestamp = Timestamp
